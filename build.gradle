subprojects {
	buildscript {
		ext {
			springVersion = "4.3.7.RELEASE"
			springBootVersion = "1.5.2.RELEASE"
			springSecurityOauthVersion = "2.0.13.RELEASE"
			openIdConnectVersion = "1.3.0"
		}
		repositories {
			mavenCentral()
			jcenter()
			maven {url "http://repo.spring.io/plugins-snapshot"}
			maven {url "http://repo.spring.io/milestone"}
			maven {url "http://repo.spring.io/release"}
		}
	}
	
	apply plugin: "eclipse"
	apply plugin: "idea" 
	apply plugin: "java"
	apply plugin: "maven"
	apply plugin: "signing"

	ext {
		baseVersion = "0.1.4"
		isSnapshot = true
		snapshotSuffix = "-SNAPSHOT"
		projectVersion = baseVersion + (isSnapshot ? snapshotSuffix : "")
	}

	group = "net.simpledynamics"
	version = projectVersion

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		mavenCentral()
		maven {url "http://oss.sonatype.org/content/groups/public"}
	}

	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc
	}

	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}
	
	artifacts {
		archives javadocJar, sourcesJar
	}

	signing {
		required {!isSnapshot && gradle.taskGraph.hasTask("uploadArchives") }  
		sign configurations.archives
	}

	project.ext.set("ossrhUsername",  project.hasProperty("ossrhUsername") ? ossrhUsername : System.getenv("SONATYPE_USERNAME"))
	project.ext.set("ossrhPassword",  project.hasProperty("ossrhPassword") ? ossrhPassword : System.getenv("SONATYPE_PASSWORD"))
	
	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
				
				//for local maven deploy testing
				//repository(url: mavenLocal().url)
					
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}
	
				snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
					authentication (userName: ossrhUsername, password: ossrhPassword)
				}
				
	
				pom.project {
					name project.name
					description project.description
					packaging 'jar'
					url 'https://github.com/simpledynamics/openid-connect-server-spring-boot'
					
					scm {
						connection 'scm:git:https://github.com/simpledynamics/openid-connect-server-spring-boot.git'
						developerConnection 'scm:git:https://github.com/simpledynamics/openid-connect-server-spring-boot.git'
						url 'https://github.com/simpledynamics/openid-connect-server-spring-boot'
					}
	
					licenses {
						license {
							name 'The Apache License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}
	
					organization {
						name 'Simple Dynamics'
						url 'http://www.simpledynamics.net/'
					}
					
					developers {
						developer {
							id 'barretttucker'
							name 'Barrett Tucker'
							email 'barrett@simpledynamics.net'
						}
						developer {
							id 'nategrover'
							name 'Nate Grover'
							email 'nate@simpledynamics.net'
						}
					}
				}
			}
		}
	}
	
}
